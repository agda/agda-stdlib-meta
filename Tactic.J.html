<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Tactic.J</title><link rel='stylesheet' href='https://cdnjs.cloudflare.com/ajax/libs/github-fork-ribbon-css/0.2.3/gh-fork-ribbon.min.css'/><style>.github-fork-ribbon:before { background-color: #333; }</style><link rel="stylesheet" href="css/Agda.css"></head><body><a class='github-fork-ribbon'href='https://github.com/agda/agda-stdlib-meta/tree/master///Tactic/J.agda'data-ribbon='Source code on Github' title='Source code on Github'>Source code on Github</a><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--safe</a> <a id="20" class="Pragma">--without-K</a> <a id="32" class="Symbol">#-}</a>

<a id="37" class="Comment">--------------------------------------------------------------------------------</a>
<a id="118" class="Comment">-- J: solve the goal by path-induction</a>
<a id="157" class="Comment">--</a>
<a id="160" class="Comment">-- this is quantified over the names of `J` and `refl`, so you should import it</a>
<a id="240" class="Comment">-- via `open import Tactic.J (quote J) (quote refl)`</a>
<a id="293" class="Comment">--------------------------------------------------------------------------------</a>

<a id="375" class="Keyword">open</a> <a id="380" class="Keyword">import</a> <a id="387" href="Meta.Init.html" class="Module">Meta.Init</a>

<a id="398" class="Keyword">module</a> <a id="405" href="Tactic.J.html" class="Module">Tactic.J</a> <a id="414" class="Symbol">(</a><a id="415" href="Tactic.J.html#415" class="Bound">J-name</a> <a id="422" href="Tactic.J.html#422" class="Bound">refl-name</a> <a id="432" class="Symbol">:</a> <a id="434" href="Agda.Builtin.Reflection.html#488" class="Postulate">Name</a><a id="438" class="Symbol">)</a> <a id="440" class="Keyword">where</a>

<a id="447" class="Keyword">open</a> <a id="452" class="Keyword">import</a> <a id="459" href="Meta.Prelude.html" class="Module">Meta.Prelude</a> <a id="472" class="Keyword">hiding</a> <a id="479" class="Symbol">(</a><a id="480" href="Relation.Binary.PropositionalEquality.Properties.html#1215" class="Function">J</a><a id="481" class="Symbol">)</a>

<a id="484" class="Keyword">open</a> <a id="489" class="Keyword">import</a> <a id="496" href="Class.Monad.html" class="Module">Class.Monad</a>
<a id="508" class="Keyword">open</a> <a id="513" class="Keyword">import</a> <a id="520" href="Class.MonadError.html" class="Module">Class.MonadError</a>
<a id="537" class="Keyword">open</a> <a id="542" class="Keyword">import</a> <a id="549" href="Class.MonadTC.html" class="Module">Class.MonadTC</a>

<a id="564" class="Keyword">open</a> <a id="569" class="Keyword">import</a> <a id="576" href="Reflection.AntiUnification.html" class="Module">Reflection.AntiUnification</a>
<a id="603" class="Keyword">open</a> <a id="608" class="Keyword">import</a> <a id="615" href="Reflection.Tactic.html" class="Module">Reflection.Tactic</a>
<a id="633" class="Keyword">open</a> <a id="638" class="Keyword">import</a> <a id="645" href="Reflection.Utils.TCI.html" class="Module">Reflection.Utils.TCI</a>

<a id="667" class="Keyword">open</a> <a id="672" href="Class.MonadTC.html#1519" class="Module">MonadTC</a> <a id="680" class="Symbol">⦃...⦄</a>
<a id="686" class="Keyword">open</a> <a id="691" href="Class.MonadError.html#248" class="Module">MonadError</a> <a id="702" class="Symbol">⦃...⦄</a>

<a id="709" class="Keyword">private</a>
  <a id="J-tac"></a><a id="719" href="Tactic.J.html#719" class="Function">J-tac</a> <a id="725" class="Symbol">:</a> <a id="727" href="Agda.Builtin.Reflection.html#488" class="Postulate">Name</a> <a id="732" class="Symbol">→</a> <a id="734" href="Agda.Builtin.Reflection.html#488" class="Postulate">Name</a> <a id="739" class="Symbol">→</a> <a id="741" class="Symbol">⦃</a> <a id="743" href="Tactic.J.html#743" class="Bound">_</a> <a id="745" class="Symbol">:</a> <a id="747" href="Class.MonadTC.html#620" class="Record">TCOptions</a> <a id="757" class="Symbol">⦄</a> <a id="759" class="Symbol">→</a> <a id="761" href="Agda.Builtin.Reflection.html#4993" class="Datatype">Term</a> <a id="766" class="Symbol">→</a> <a id="768" href="Agda.Builtin.Reflection.html#4993" class="Datatype">Term</a> <a id="773" class="Symbol">→</a> <a id="775" href="Reflection.Tactic.html#453" class="Function">Tactic</a>
  <a id="784" href="Tactic.J.html#719" class="Function">J-tac</a> <a id="790" href="Tactic.J.html#790" class="Bound">J-name</a> <a id="797" href="Tactic.J.html#797" class="Bound">refl-name</a> <a id="807" href="Tactic.J.html#807" class="Bound">t</a> <a id="809" href="Tactic.J.html#809" class="Bound">t&#39;</a> <a id="812" class="Symbol">=</a> <a id="814" href="Reflection.Tactic.html#801" class="Function">initTac</a> <a id="822" href="Function.Base.html#1974" class="Function Operator">$</a> <a id="824" href="Reflection.Utils.TCI.html#5895" class="Function">inDebugPath</a> <a id="836" class="String">&quot;J&quot;</a> <a id="840" class="Keyword">do</a>
    <a id="847" href="Tactic.J.html#847" class="Bound">ty</a> <a id="850" href="Class.Monad.Core.html#299" class="Field Operator">←</a> <a id="852" href="Class.MonadTC.html#11576" class="Function">goalTy</a>
    <a id="863" href="Class.MonadTC.html#8199" class="Function">debugLog</a> <a id="872" class="Symbol">(</a><a id="873" class="String">&quot;anti-unify &quot;</a> <a id="887" href="Reflection.Debug.html#1067" class="Function Operator">∷ᵈ</a> <a id="890" href="Tactic.J.html#847" class="Bound">ty</a> <a id="893" href="Reflection.Debug.html#1067" class="Function Operator">∷ᵈ</a> <a id="896" href="Agda.Builtin.List.html#184" class="InductiveConstructor">[]</a><a id="898" class="Symbol">)</a>
    <a id="904" href="Tactic.J.html#904" class="Bound">ty&#39;</a> <a id="908" href="Class.Monad.Core.html#299" class="Field Operator">←</a> <a id="910" href="Class.MonadTC.html#1736" class="Field">inferType</a> <a id="920" href="Tactic.J.html#807" class="Bound">t</a>
    <a id="926" href="Class.MonadTC.html#8199" class="Function">debugLog</a> <a id="935" class="Symbol">(</a><a id="936" class="String">&quot;with:      &quot;</a> <a id="950" href="Reflection.Debug.html#1067" class="Function Operator">∷ᵈ</a> <a id="953" href="Tactic.J.html#904" class="Bound">ty&#39;</a> <a id="957" href="Reflection.Debug.html#1067" class="Function Operator">∷ᵈ</a> <a id="960" href="Agda.Builtin.List.html#184" class="InductiveConstructor">[]</a><a id="962" class="Symbol">)</a>
    <a id="968" class="Keyword">let</a> <a id="972" href="Tactic.J.html#972" class="Bound">gen</a> <a id="976" class="Symbol">=</a> <a id="978" href="Reflection.Syntax.html#2287" class="InductiveConstructor Operator">`λ⟦</a> <a id="982" class="String">&quot;x&quot;</a> <a id="986" href="Reflection.Syntax.html#2287" class="InductiveConstructor Operator">∣</a> <a id="988" class="String">&quot;y&quot;</a> <a id="992" href="Reflection.Syntax.html#2287" class="InductiveConstructor Operator">⇒</a> <a id="994" href="Reflection.AntiUnification.html#1886" class="Function">antiUnify</a> <a id="1004" class="Number">0</a> <a id="1006" href="Tactic.J.html#847" class="Bound">ty</a> <a id="1009" href="Tactic.J.html#904" class="Bound">ty&#39;</a> <a id="1013" href="Reflection.Syntax.html#2287" class="InductiveConstructor Operator">⟧</a>
    <a id="1019" href="Class.MonadTC.html#8199" class="Function">debugLog</a> <a id="1028" class="Symbol">(</a><a id="1029" href="Tactic.J.html#972" class="Bound">gen</a> <a id="1033" href="Reflection.Debug.html#1067" class="Function Operator">∷ᵈ</a> <a id="1036" href="Agda.Builtin.List.html#184" class="InductiveConstructor">[]</a><a id="1038" class="Symbol">)</a>
    <a id="1044" href="Class.MonadTC.html#12947" class="Function">unifyWithGoal</a> <a id="1058" class="Symbol">(</a><a id="1059" href="Tactic.J.html#790" class="Bound">J-name</a> <a id="1066" href="Reflection.Syntax.html#3017" class="InductiveConstructor Operator">∙⟦</a> <a id="1069" href="Tactic.J.html#972" class="Bound">gen</a> <a id="1073" href="Reflection.Syntax.html#3017" class="InductiveConstructor Operator">∣</a> <a id="1075" href="Tactic.J.html#807" class="Bound">t</a> <a id="1077" href="Reflection.Syntax.html#3017" class="InductiveConstructor Operator">∣</a> <a id="1079" href="Tactic.J.html#809" class="Bound">t&#39;</a> <a id="1082" href="Reflection.Syntax.html#3017" class="InductiveConstructor Operator">⟧</a><a id="1083" class="Symbol">)</a>
    <a id="1089" class="Keyword">where</a>
      <a id="1101" href="Tactic.J.html#1101" class="Function">reflHelper</a> <a id="1112" class="Symbol">:</a> <a id="1114" href="Agda.Builtin.Reflection.html#4993" class="Datatype">Term</a> <a id="1119" class="Symbol">→</a> <a id="1121" href="Agda.Builtin.Reflection.html#4993" class="Datatype">Term</a> <a id="1126" class="Symbol">→</a> <a id="1128" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a>
      <a id="1136" href="Tactic.J.html#1101" class="Function">reflHelper</a> <a id="1147" class="Symbol">_</a> <a id="1149" class="Symbol">(</a><a id="1150" href="Agda.Builtin.Reflection.html#5194" class="InductiveConstructor">con</a> <a id="1154" href="Tactic.J.html#1154" class="Bound">refl-name</a> <a id="1164" class="Symbol">_)</a> <a id="1167" class="Symbol">=</a> <a id="1169" class="Number">0</a>
      <a id="1177" href="Tactic.J.html#1101" class="CatchallClause Function">reflHelper</a><a id="1187" class="CatchallClause"> </a><a id="1188" class="CatchallClause Symbol">_</a><a id="1189" class="CatchallClause"> </a><a id="1190" class="CatchallClause Symbol">_</a> <a id="1192" class="Symbol">=</a> <a id="1194" class="Number">1</a>

      <a id="1203" class="Keyword">open</a> <a id="1208" href="Reflection.AntiUnification.html#1801" class="Module">AntiUnification</a> <a id="1224" href="Tactic.J.html#1101" class="Function">reflHelper</a> <a id="1235" class="Number">2</a>

<a id="1238" class="Keyword">macro</a>
  <a id="by-J"></a><a id="1246" href="Tactic.J.html#1246" class="Function">by-J</a> <a id="1251" class="Symbol">=</a> <a id="1253" href="Tactic.J.html#719" class="Function">J-tac</a> <a id="1259" href="Tactic.J.html#415" class="Bound">J-name</a> <a id="1266" href="Tactic.J.html#422" class="Bound">refl-name</a>

<a id="1277" class="Keyword">private</a>
  <a id="1287" class="Keyword">module</a> <a id="Test"></a><a id="1294" href="Tactic.J.html#1294" class="Module">Test</a> <a id="1299" class="Keyword">where</a>
    <a id="1309" class="Keyword">open</a> <a id="1314" class="Keyword">import</a> <a id="1321" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a> <a id="1359" class="Keyword">hiding</a> <a id="1366" class="Symbol">(</a><a id="1367" href="Relation.Binary.PropositionalEquality.Properties.html#1215" class="Function">J</a><a id="1368" class="Symbol">)</a>
    <a id="1374" class="Keyword">open</a> <a id="1379" href="Relation.Binary.PropositionalEquality.Properties.html#6731" class="Module">≡-Reasoning</a>
    <a id="1395" class="Keyword">open</a> <a id="1400" class="Keyword">import</a> <a id="1407" href="Tactic.Defaults.html" class="Module">Tactic.Defaults</a>

    <a id="1428" class="Comment">-- since we can&#39;t use names abstractly, we need to do some yoga here</a>

    <a id="1502" class="Keyword">private</a> <a id="1510" class="Keyword">variable</a>
      <a id="1525" href="Tactic.J.html#1525" class="Generalizable">x</a> <a id="1527" href="Tactic.J.html#1527" class="Generalizable">y</a> <a id="1529" href="Tactic.J.html#1529" class="Generalizable">z</a> <a id="1531" href="Tactic.J.html#1531" class="Generalizable">w</a> <a id="1533" class="Symbol">:</a> <a id="1535" href="Meta.Prelude.html#195" class="Generalizable">A</a>

    <a id="Test.J"></a><a id="1542" href="Tactic.J.html#1542" class="Function">J</a> <a id="1544" class="Symbol">:</a> <a id="1546" class="Symbol">∀</a> <a id="1548" class="Symbol">{</a><a id="1549" href="Tactic.J.html#1549" class="Bound">ℓ₁</a> <a id="1552" href="Tactic.J.html#1552" class="Bound">ℓ₂</a><a id="1554" class="Symbol">}</a> <a id="1556" class="Symbol">{</a><a id="1557" href="Tactic.J.html#1557" class="Bound">A</a> <a id="1559" class="Symbol">:</a> <a id="1561" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1565" href="Tactic.J.html#1549" class="Bound">ℓ₁</a><a id="1567" class="Symbol">}</a> <a id="1569" class="Symbol">{</a><a id="1570" href="Tactic.J.html#1570" class="Bound">x</a> <a id="1572" class="Symbol">:</a> <a id="1574" href="Tactic.J.html#1557" class="Bound">A</a><a id="1575" class="Symbol">}</a> <a id="1577" class="Symbol">(</a><a id="1578" href="Tactic.J.html#1578" class="Bound">P</a> <a id="1580" class="Symbol">:</a> <a id="1582" class="Symbol">(</a><a id="1583" href="Tactic.J.html#1583" class="Bound">y</a> <a id="1585" class="Symbol">:</a> <a id="1587" href="Tactic.J.html#1557" class="Bound">A</a><a id="1588" class="Symbol">)</a> <a id="1590" class="Symbol">→</a> <a id="1592" href="Tactic.J.html#1570" class="Bound">x</a> <a id="1594" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">≡</a> <a id="1596" href="Tactic.J.html#1583" class="Bound">y</a> <a id="1598" class="Symbol">→</a> <a id="1600" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1604" href="Tactic.J.html#1552" class="Bound">ℓ₂</a><a id="1606" class="Symbol">)</a>
      <a id="1614" class="Symbol">→</a> <a id="1616" href="Tactic.J.html#1578" class="Bound">P</a> <a id="1618" href="Tactic.J.html#1570" class="Bound">x</a> <a id="1620" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a> <a id="1625" class="Symbol">→</a> <a id="1627" class="Symbol">{</a><a id="1628" href="Tactic.J.html#1628" class="Bound">y</a> <a id="1630" class="Symbol">:</a> <a id="1632" href="Tactic.J.html#1557" class="Bound">A</a><a id="1633" class="Symbol">}</a> <a id="1635" class="Symbol">(</a><a id="1636" href="Tactic.J.html#1636" class="Bound">p</a> <a id="1638" class="Symbol">:</a> <a id="1640" href="Tactic.J.html#1570" class="Bound">x</a> <a id="1642" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">≡</a> <a id="1644" href="Tactic.J.html#1628" class="Bound">y</a><a id="1645" class="Symbol">)</a> <a id="1647" class="Symbol">→</a> <a id="1649" href="Tactic.J.html#1578" class="Bound">P</a> <a id="1651" href="Tactic.J.html#1628" class="Bound">y</a> <a id="1653" href="Tactic.J.html#1636" class="Bound">p</a>
    <a id="1659" href="Tactic.J.html#1542" class="Function">J</a> <a id="1661" class="Symbol">_</a> <a id="1663" href="Tactic.J.html#1663" class="Bound">Px</a> <a id="1666" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a> <a id="1671" class="Symbol">=</a> <a id="1673" href="Tactic.J.html#1663" class="Bound">Px</a>

    <a id="1681" class="Keyword">macro</a>
      <a id="Test.by-Jᵗ"></a><a id="1693" href="Tactic.J.html#1693" class="Function">by-Jᵗ</a> <a id="1699" class="Symbol">=</a> <a id="1701" href="Tactic.J.html#719" class="Function">J-tac</a> <a id="1707" class="Symbol">(</a><a id="1708" class="Keyword">quote</a> <a id="1714" href="Tactic.J.html#1542" class="Function">J</a><a id="1715" class="Symbol">)</a> <a id="1717" class="Symbol">(</a><a id="1718" class="Keyword">quote</a> <a id="1724" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a><a id="1728" class="Symbol">)</a>

    <a id="1735" class="Comment">-- in this case, by-Jᵗ needs a type annotation or it can&#39;t anti-unify properly</a>
    <a id="Test._∙_"></a><a id="1818" href="Tactic.J.html#1818" class="Function Operator">_∙_</a> <a id="1822" class="Symbol">:</a> <a id="1824" href="Tactic.J.html#1525" class="Generalizable">x</a> <a id="1826" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">≡</a> <a id="1828" href="Tactic.J.html#1527" class="Generalizable">y</a> <a id="1830" class="Symbol">→</a> <a id="1832" href="Tactic.J.html#1527" class="Generalizable">y</a> <a id="1834" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">≡</a> <a id="1836" href="Tactic.J.html#1529" class="Generalizable">z</a> <a id="1838" class="Symbol">→</a> <a id="1840" href="Tactic.J.html#1525" class="Generalizable">x</a> <a id="1842" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">≡</a> <a id="1844" href="Tactic.J.html#1529" class="Generalizable">z</a>
    <a id="1850" href="Tactic.J.html#1818" class="Function Operator">_∙_</a> <a id="1854" class="Symbol">{</a><a id="1855" class="Argument">x</a> <a id="1857" class="Symbol">=</a> <a id="1859" href="Tactic.J.html#1859" class="Bound">x</a><a id="1860" class="Symbol">}</a> <a id="1862" class="Symbol">{</a><a id="1863" href="Tactic.J.html#1863" class="Bound">y</a><a id="1864" class="Symbol">}</a> <a id="1866" class="Symbol">{</a><a id="1867" href="Tactic.J.html#1867" class="Bound">z</a><a id="1868" class="Symbol">}</a> <a id="1870" href="Tactic.J.html#1870" class="Bound">p</a> <a id="1872" class="Symbol">=</a> <a id="1874" href="Tactic.J.html#1693" class="Macro">by-Jᵗ</a> <a id="1880" class="Symbol">(λ</a> <a id="1883" href="Tactic.J.html#1883" class="Bound">q</a> <a id="1885" class="Symbol">→</a> <a id="1887" href="Tactic.J.html#1859" class="Bound">x</a> <a id="1889" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">≡</a> <a id="1891" href="Tactic.J.html#1867" class="Bound">z</a> <a id="1893" href="Function.Base.html#4486" class="Function Operator">∋</a> <a id="1895" href="Tactic.J.html#1883" class="Bound">q</a><a id="1896" class="Symbol">)</a> <a id="1898" href="Tactic.J.html#1870" class="Bound">p</a>

    <a id="Test.∙-id-l"></a><a id="1905" href="Tactic.J.html#1905" class="Function">∙-id-l</a> <a id="1912" class="Symbol">:</a> <a id="1914" class="Symbol">(</a><a id="1915" href="Tactic.J.html#1915" class="Bound">p</a> <a id="1917" class="Symbol">:</a> <a id="1919" href="Tactic.J.html#1527" class="Generalizable">y</a> <a id="1921" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">≡</a> <a id="1923" href="Tactic.J.html#1529" class="Generalizable">z</a><a id="1924" class="Symbol">)</a> <a id="1926" class="Symbol">→</a> <a id="1928" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a> <a id="1933" href="Tactic.J.html#1818" class="Function Operator">∙</a> <a id="1935" href="Tactic.J.html#1915" class="Bound">p</a> <a id="1937" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">≡</a> <a id="1939" href="Tactic.J.html#1915" class="Bound">p</a>
    <a id="1945" href="Tactic.J.html#1905" class="Function">∙-id-l</a> <a id="1952" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a> <a id="1957" class="Symbol">=</a> <a id="1959" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a>

    <a id="Test.∙-assoc"></a><a id="1969" href="Tactic.J.html#1969" class="Function">∙-assoc</a> <a id="1977" class="Symbol">:</a> <a id="1979" class="Symbol">(</a><a id="1980" href="Tactic.J.html#1980" class="Bound">p</a> <a id="1982" class="Symbol">:</a> <a id="1984" href="Tactic.J.html#1531" class="Generalizable">w</a> <a id="1986" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">≡</a> <a id="1988" href="Tactic.J.html#1525" class="Generalizable">x</a><a id="1989" class="Symbol">)</a> <a id="1991" class="Symbol">(</a><a id="1992" href="Tactic.J.html#1992" class="Bound">q</a> <a id="1994" class="Symbol">:</a> <a id="1996" href="Tactic.J.html#1525" class="Generalizable">x</a> <a id="1998" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">≡</a> <a id="2000" href="Tactic.J.html#1527" class="Generalizable">y</a><a id="2001" class="Symbol">)</a> <a id="2003" class="Symbol">(</a><a id="2004" href="Tactic.J.html#2004" class="Bound">r</a> <a id="2006" class="Symbol">:</a> <a id="2008" href="Tactic.J.html#1527" class="Generalizable">y</a> <a id="2010" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">≡</a> <a id="2012" href="Tactic.J.html#1529" class="Generalizable">z</a><a id="2013" class="Symbol">)</a> <a id="2015" class="Symbol">→</a> <a id="2017" href="Tactic.J.html#1980" class="Bound">p</a> <a id="2019" href="Tactic.J.html#1818" class="Function Operator">∙</a> <a id="2021" class="Symbol">(</a><a id="2022" href="Tactic.J.html#1992" class="Bound">q</a> <a id="2024" href="Tactic.J.html#1818" class="Function Operator">∙</a> <a id="2026" href="Tactic.J.html#2004" class="Bound">r</a><a id="2027" class="Symbol">)</a> <a id="2029" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">≡</a> <a id="2031" class="Symbol">(</a><a id="2032" href="Tactic.J.html#1980" class="Bound">p</a> <a id="2034" href="Tactic.J.html#1818" class="Function Operator">∙</a> <a id="2036" href="Tactic.J.html#1992" class="Bound">q</a><a id="2037" class="Symbol">)</a> <a id="2039" href="Tactic.J.html#1818" class="Function Operator">∙</a> <a id="2041" href="Tactic.J.html#2004" class="Bound">r</a>
    <a id="2047" href="Tactic.J.html#1969" class="Function">∙-assoc</a> <a id="2055" class="Symbol">{</a><a id="2056" class="Argument">w</a> <a id="2058" class="Symbol">=</a> <a id="2060" href="Tactic.J.html#2060" class="Bound">w</a><a id="2061" class="Symbol">}</a> <a id="2063" class="Symbol">{</a><a id="2064" href="Tactic.J.html#2064" class="Bound">x</a><a id="2065" class="Symbol">}</a> <a id="2067" class="Symbol">{</a><a id="2068" href="Tactic.J.html#2068" class="Bound">y</a><a id="2069" class="Symbol">}</a> <a id="2071" class="Symbol">{</a><a id="2072" href="Tactic.J.html#2072" class="Bound">z</a><a id="2073" class="Symbol">}</a> <a id="2075" href="Tactic.J.html#2075" class="Bound">p</a> <a id="2077" class="Symbol">=</a> <a id="2079" href="Tactic.J.html#1693" class="Macro">by-Jᵗ</a> <a id="2085" href="Tactic.J.html#2113" class="Function">lemma</a> <a id="2091" href="Tactic.J.html#2075" class="Bound">p</a>
      <a id="2099" class="Keyword">where</a>
        <a id="2113" href="Tactic.J.html#2113" class="Function">lemma</a> <a id="2119" class="Symbol">:</a> <a id="2121" class="Symbol">(</a><a id="2122" href="Tactic.J.html#2122" class="Bound">q</a> <a id="2124" class="Symbol">:</a> <a id="2126" href="Tactic.J.html#2060" class="Bound">w</a> <a id="2128" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">≡</a> <a id="2130" href="Tactic.J.html#2068" class="Bound">y</a><a id="2131" class="Symbol">)</a> <a id="2133" class="Symbol">(</a><a id="2134" href="Tactic.J.html#2134" class="Bound">r</a> <a id="2136" class="Symbol">:</a> <a id="2138" href="Tactic.J.html#2068" class="Bound">y</a> <a id="2140" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">≡</a> <a id="2142" href="Tactic.J.html#2072" class="Bound">z</a><a id="2143" class="Symbol">)</a> <a id="2145" class="Symbol">→</a> <a id="2147" class="Symbol">(</a><a id="2148" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a> <a id="2153" href="Tactic.J.html#1818" class="Function Operator">∙</a> <a id="2155" class="Symbol">(</a><a id="2156" href="Tactic.J.html#2122" class="Bound">q</a> <a id="2158" href="Tactic.J.html#1818" class="Function Operator">∙</a> <a id="2160" href="Tactic.J.html#2134" class="Bound">r</a><a id="2161" class="Symbol">))</a> <a id="2164" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">≡</a> <a id="2166" class="Symbol">((</a><a id="2168" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a> <a id="2173" href="Tactic.J.html#1818" class="Function Operator">∙</a> <a id="2175" href="Tactic.J.html#2122" class="Bound">q</a><a id="2176" class="Symbol">)</a> <a id="2178" href="Tactic.J.html#1818" class="Function Operator">∙</a> <a id="2180" href="Tactic.J.html#2134" class="Bound">r</a><a id="2181" class="Symbol">)</a>
        <a id="2191" href="Tactic.J.html#2113" class="Function">lemma</a> <a id="2197" href="Tactic.J.html#2197" class="Bound">q</a> <a id="2199" href="Tactic.J.html#2199" class="Bound">r</a> <a id="2201" class="Symbol">=</a> <a id="2203" href="Relation.Binary.Reasoning.Syntax.html#1572" class="Function Operator">begin</a>
          <a id="2219" class="Symbol">(</a><a id="2220" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a> <a id="2225" href="Tactic.J.html#1818" class="Function Operator">∙</a> <a id="2227" class="Symbol">(</a><a id="2228" href="Tactic.J.html#2197" class="Bound">q</a> <a id="2230" href="Tactic.J.html#1818" class="Function Operator">∙</a> <a id="2232" href="Tactic.J.html#2199" class="Bound">r</a><a id="2233" class="Symbol">))</a> <a id="2236" href="Relation.Binary.Reasoning.Syntax.html#11048" class="Function">≡⟨</a> <a id="2239" href="Tactic.J.html#1905" class="Function">∙-id-l</a> <a id="2246" class="Symbol">(</a><a id="2247" href="Tactic.J.html#2197" class="Bound">q</a> <a id="2249" href="Tactic.J.html#1818" class="Function Operator">∙</a> <a id="2251" href="Tactic.J.html#2199" class="Bound">r</a><a id="2252" class="Symbol">)</a> <a id="2254" href="Relation.Binary.Reasoning.Syntax.html#11048" class="Function">⟩</a>
          <a id="2266" href="Tactic.J.html#2197" class="Bound">q</a> <a id="2268" href="Tactic.J.html#1818" class="Function Operator">∙</a> <a id="2270" href="Tactic.J.html#2199" class="Bound">r</a>            <a id="2283" href="Relation.Binary.Reasoning.Syntax.html#11539" class="Function">≡˘⟨</a> <a id="2287" href="Relation.Binary.PropositionalEquality.Core.html#1339" class="Function">cong</a> <a id="2292" class="Symbol">(λ</a> <a id="2295" href="Tactic.J.html#2295" class="Bound">x</a> <a id="2297" class="Symbol">→</a> <a id="2299" href="Tactic.J.html#2295" class="Bound">x</a> <a id="2301" href="Tactic.J.html#1818" class="Function Operator">∙</a> <a id="2303" href="Tactic.J.html#2199" class="Bound">r</a><a id="2304" class="Symbol">)</a> <a id="2306" class="Symbol">(</a><a id="2307" href="Tactic.J.html#1905" class="Function">∙-id-l</a> <a id="2314" href="Tactic.J.html#2197" class="Bound">q</a><a id="2315" class="Symbol">)</a> <a id="2317" href="Relation.Binary.Reasoning.Syntax.html#11539" class="Function">⟩</a>
          <a id="2329" class="Symbol">(</a><a id="2330" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a> <a id="2335" href="Tactic.J.html#1818" class="Function Operator">∙</a> <a id="2337" href="Tactic.J.html#2197" class="Bound">q</a><a id="2338" class="Symbol">)</a> <a id="2340" href="Tactic.J.html#1818" class="Function Operator">∙</a> <a id="2342" href="Tactic.J.html#2199" class="Bound">r</a>   <a id="2346" href="Relation.Binary.Reasoning.Syntax.html#12345" class="Function Operator">∎</a>
</pre></body></html>